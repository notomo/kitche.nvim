
call themis#option('recursive', 1)

let s:root = getcwd()
let s:test_data_dir = s:root . '/test/_test_data/'

function! KitcheTestHelper() abort
    let helper = {
        \ 'assert': s:assert(),
    \ }

    function! helper.before_each() abort
        execute 'cd' s:test_data_dir

        call kitche#messenger#set_func({ msg -> themis#log('[test messenger] ' . msg) })
        call themis#log('')

        filetype on
        syntax enable
    endfunction

    function! helper.after_each() abort
        tabedit
        tabonly!
        silent! %bwipeout!

        filetype off
        syntax off
    endfunction

    function! helper.suite(name) abort
        let suite = themis#suite(a:name)
        let suite.before_each = self.before_each
        let suite.after_each = self.after_each
        return suite
    endfunction

    function! helper.search(pattern) abort
        let result = search(a:pattern)
        if result == 0
            let message = printf('%s not found', a:pattern)
            call self.assert.fail(message)
        endif
        return result
    endfunction

    function! helper.buffer_log() abort
        let lines = getbufline('%', 1, '$')
        for line in lines
            call themis#log('[buffer] ' . line)
        endfor
    endfunction

    return helper
endfunction

function! s:assert() abort
    let assert = themis#helper('assert')

    function! assert.window_count(count) abort
        let window_count = tabpagewinnr(tabpagenr(), '$')
        let message = printf('window count must be %s, but actual: %s', a:count, window_count)
        call self.equals(window_count, a:count, message)
    endfunction

    function! assert.filetype(expected) abort
        let actual = &filetype
        let message = printf('buffer &filetype should be %s, but actual: %s', a:expected, actual)
        call self.equals(a:expected, actual, message)
    endfunction

    function! assert.current_line(expected) abort
        let actual = getline(line('.'))
        let message = printf('current line should be %s, but actual: %s', a:expected, actual)
        call self.equals(actual, a:expected, message)
    endfunction

    function! assert.buftype(expected) abort
        let actual = &buftype
        let message = printf('buftype should be %s, but actual: %s', a:expected, actual)
        call self.equals(actual, a:expected, message)
    endfunction

    function! assert.not_found(pattern) abort
        let result = search(a:pattern, 'nw')
        let message = printf('"%s" should not be found, but found at line: %s', a:pattern, result)
        call self.false(result, message)
    endfunction

    function! assert.found(pattern) abort
        let result = search(a:pattern, 'nw')
        let message = printf('"%s" should be found, but not found', a:pattern)
        call self.truthy(result, message)
    endfunction

    function! assert.file_name(expected) abort
        let actual = fnamemodify(bufname('%'), ':t')
        let message = printf('file name should be %s, but actual: %s', a:expected, actual)
        call self.equals(actual, a:expected, message)
    endfunction

    function! assert.tab_count(expected) abort
        let actual = tabpagenr('$')
        let message = printf('tab count should be %s, but actual: %s', a:expected, actual)
        call self.equals(actual, a:expected, message)
    endfunction

    function! assert.line_count(expected) abort
        let actual = line('$')
        let message = printf('line count should be %s, but actual: %s', a:expected, actual)
        call self.equals(actual, a:expected, message)
    endfunction

    return assert
endfunction
